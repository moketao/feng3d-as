package me.feng3d.core.base.submesh
{
	import flash.events.Event;
	import flash.geom.Matrix3D;

	import me.feng3d.arcane;
	import me.feng3d.animators.IAnimator;
	import me.feng3d.animators.base.data.AnimationSubGeometry;
	import me.feng3d.cameras.Camera3D;
	import me.feng3d.core.base.Context3DBufferOwner;
	import me.feng3d.core.base.IRenderable;
	import me.feng3d.core.base.subgeometry.SubGeometry;
	import me.feng3d.core.buffer.Context3DCache;
	import me.feng3d.core.buffer.Context3DCacheFeng3d;
	import me.feng3d.core.proxy.Stage3DProxy;
	import me.feng3d.entities.Entity;
	import me.feng3d.entities.Mesh;
	import me.feng3d.events.MeshEvent;
	import me.feng3d.materials.MaterialBase;

	use namespace arcane;

	/**
	 * 子网格，可渲染对象
	 */
	public class SubMesh extends Context3DBufferOwner implements IRenderable
	{
		protected var _parentMesh:Mesh;
		protected var _subGeometry:SubGeometry;
		arcane var _index:uint;

		protected var _materialSelf:MaterialBase;
		private var _material:MaterialBase;
		private var _materialDirty:Boolean;

		private var _animator:IAnimator;

		private var _animationSubGeometry:AnimationSubGeometry;

		private var _context3dCache:Context3DCache;

		/**
		 * 创建一个子网格
		 * @param subGeometry 子几何体
		 * @param parentMesh 父网格
		 * @param material 材质
		 */
		public function SubMesh(subGeometry:SubGeometry, parentMesh:Mesh, material:MaterialBase = null)
		{
			_parentMesh = parentMesh;
			this.subGeometry = subGeometry;
			this.material = material;

			_context3dCache = new Context3DCacheFeng3d()
			activateContext3DBuffer(_context3dCache);
			_parentMesh.addEventListener(MeshEvent.MATERIAL_CHANGE, onMaterialChange);
		}

		/**
		 * @inheritDoc
		 */
		public function get context3dCache():Context3DCache
		{
			return _context3dCache;
		}

		/**
		 * 渲染材质
		 */
		public function get material():MaterialBase
		{
			if (_materialDirty)
				updateMaterial();
			return _material;
		}

		/**
		 * 自身材质
		 */
		public function get materialSelf():MaterialBase
		{
			return _materialSelf;
		}

		public function set material(value:MaterialBase):void
		{
			_materialSelf = value;
			_materialDirty = true;
		}

		/**
		 * 更新材质
		 */
		private function updateMaterial():void
		{
			var value:MaterialBase = _materialSelf ? _materialSelf : _parentMesh.material;
			if (value == _material)
				return;

			if (_material)
			{
				removeChildBufferOwner(_material);
				_material.removeOwner(this);
			}
			_material = value;
			if (_material)
			{
				addChildBufferOwner(_material);
				_material.addOwner(this);
			}
		}

		/**
		 * 所属实体
		 */
		public function get sourceEntity():Entity
		{
			return _parentMesh;
		}

		/**
		 * 子网格
		 */
		public function get subGeometry():SubGeometry
		{
			return _subGeometry;
		}

		public function set subGeometry(value:SubGeometry):void
		{
			if (_subGeometry)
			{
				removeChildBufferOwner(_subGeometry);
			}
			_subGeometry = value;
			if (_subGeometry)
			{
				addChildBufferOwner(_subGeometry);
			}
		}

		/**
		 * 动画顶点数据(例如粒子特效的时间、位置偏移、速度等等)
		 */
		public function get animationSubGeometry():AnimationSubGeometry
		{
			return _animationSubGeometry;
		}

		public function set animationSubGeometry(value:AnimationSubGeometry):void
		{
			if (_animationSubGeometry)
			{
				removeChildBufferOwner(_animationSubGeometry);
			}
			_animationSubGeometry = value;
			if (_animationSubGeometry)
			{
				addChildBufferOwner(_animationSubGeometry);
			}
		}

		/**
		 * @inheritDoc
		 */
		public function get animator():IAnimator
		{
			return _animator;
		}

		public function set animator(value:IAnimator):void
		{
			if (_animator)
			{
				removeChildBufferOwner(_animator);
				material.animationSet = null;
			}
			_animator = value;
			if (_animator)
			{
				addChildBufferOwner(_animator);
				material.animationSet = _animator.animationSet;
			}
		}

		/**
		 * 父网格
		 */
		arcane function get parentMesh():Mesh
		{
			return _parentMesh;
		}

		/**
		 * @inheritDoc
		 */
		public function get mouseEnabled():Boolean
		{
			return _parentMesh.mouseEnabled || _parentMesh.ancestorsAllowMouseEnabled;
		}

		/**
		 * @inheritDoc
		 */
		public function render(stage3DProxy:Stage3DProxy, camera:Camera3D):void
		{
			if (!_parentMesh.visible)
				return;

			//初始化渲染参数
			context3dCache.shaderParams.init();

			material.activatePass(context3dCache.shaderParams, stage3DProxy, camera);
			//准备渲染时所需数据 与 设置渲染参数
			material.renderPass(this, stage3DProxy, camera);

			//绘制图形
			context3dCache.render(stage3DProxy.context3D);
		}

		/**
		 * @inheritDoc
		 */
		public function getRenderSceneTransform(camera:Camera3D):Matrix3D
		{
			return _parentMesh.sceneTransform;
		}

		/**
		 * @inheritDoc
		 */
		public function get numTriangles():uint
		{
			return _subGeometry.numTriangles;
		}

		/**
		 * 处理材质变化事件
		 */
		private function onMaterialChange(event:Event):void
		{
			_materialDirty = true;
		}

		/**
		 * 销毁
		 */
		public function dispose():void
		{
			material = null;
		}
	}
}
