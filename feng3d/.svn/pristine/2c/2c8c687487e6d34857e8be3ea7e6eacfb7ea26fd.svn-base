package me.feng3d.materials.methods
{
	import me.feng3d.library.assets.AssetType;
	import me.feng3d.library.assets.IAsset;
	import me.feng3d.lights.LightBase;
	import me.feng3d.lights.shadowmaps.ShadowMapperBase;

	/**
	 * ShadowMapMethodBase 为阴影映射方法提供一个抽象基类
	 * @author warden_feng 2015-2-28
	 */
	public class ShadowMapMethodBase extends ShadingMethodBase implements IAsset
	{
		protected var _castingLight:LightBase;
		protected var _shadowMapper:ShadowMapperBase;
		
		protected var _epsilon:Number = .02;
		
		/**
		 * Creates a new ShadowMapMethodBase object.
		 * @param castingLight The light used to cast shadows.
		 */
		public function ShadowMapMethodBase(castingLight:LightBase)
		{
			super();
			_castingLight = castingLight;
			castingLight.castsShadows = true;
			_shadowMapper = castingLight.shadowMapper;
		}

		/**
		 * The light casting the shadows.
		 */
		public function get castingLight():LightBase
		{
			return _castingLight;
		}
		
		/**
		 * A small value to counter floating point precision errors when comparing values in the shadow map with the
		 * calculated depth value. Increase this if shadow banding occurs, decrease it if the shadow seems to be too detached.
		 */
		public function get epsilon():Number
		{
			return _epsilon;
		}
		
		public function set epsilon(value:Number):void
		{
			_epsilon = value;
		}
		
		public function get assetType():String
		{
			return AssetType.SHADOW_MAP_METHOD;
		}
	}
}
